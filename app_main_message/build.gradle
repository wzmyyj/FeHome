def runAlone = rootProject.ext.runAlone.main_message.toBoolean()

if (runAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    resourcePrefix "main_message_"

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        if (runAlone) {
            applicationId "top.wzmyyj.main_message"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionName rootProject.ext.app.versionName
        versionCode getVersionCodeFromName(versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        multiDexEnabled true
    }

    kapt {
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (runAlone) {
                manifest.srcFile 'src/main/runalone/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'src/main/runalone/*'
                }
            }
        }
    }

    flavorDimensions "channel"

    productFlavors {
        // 日常测试
        daily {}
        // 灰度
        gray {}
        // 官方渠道
        official {}
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies.kotlin_jdk
    implementation rootProject.ext.dependencies.androidx_appcompat
    implementation rootProject.ext.dependencies.androidx_core
    implementation rootProject.ext.dependencies.androidx_constraintlayout

    testImplementation rootProject.ext.dependencies.junit
    androidTestImplementation rootProject.ext.dependencies.test_runner
    androidTestImplementation rootProject.ext.dependencies.test_espresso

    implementation rootProject.ext.dependencies.arouter_api
    kapt rootProject.ext.dependencies.arouter_compiler

    api project(':common')
    api project(':common_service')

}
