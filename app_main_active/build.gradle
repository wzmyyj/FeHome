def runAlone = rootProject.ext.runAlone.main_active.toBoolean()

if (runAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    // 所有xml资源命名以 "live_" 开头、否则编译报红
    resourcePrefix "main_active_"

    ext.VersionName = "1.0"

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        // applicationId ApplicationId
        if (runAlone) {
            // 单独调试时添加 applicationId ，集成调试时移除
            applicationId "top.wzmyyj.main_active"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode getCodeFromName(VersionName)
        versionName VersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (runAlone) {
                manifest.srcFile 'src/main/runalone/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'src/main/runalone/*'
                }
            }
        }
    }

    flavorDimensions "channel"

    productFlavors {
        // 日常测试
        daily {}
        // 灰度
        gray {}
        // 官方渠道
        official {}
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies.kotlin_jdk
    implementation rootProject.ext.dependencies.androidx_appcompat
    implementation rootProject.ext.dependencies.androidx_core
    implementation rootProject.ext.dependencies.androidx_constraintlayout

    testImplementation rootProject.ext.dependencies.junit
    androidTestImplementation rootProject.ext.dependencies.test_runner
    androidTestImplementation rootProject.ext.dependencies.test_espresso
}
